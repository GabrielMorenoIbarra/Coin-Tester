plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

repositories {
    google()
    mavenCentral()
}

android {
    Properties properties = new Properties()
    properties.load(project.file('../local.properties').newDataInputStream())

    signingConfigs {
        release {
            keyAlias properties.getProperty('keystore.keyAlias')
            storeFile file(properties.getProperty('keystore.storeFile'))
            storePassword properties.getProperty('keystore.storePassword')
            keyPassword properties.getProperty('keystore.keyPassword')
        }
    }

    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.appacoustic.cointester"
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.versionCode
        versionName project.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    bundle {
        density { enableSplit true }
        abi { enableSplit true }
        language { enableSplit false }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            splits.abi.enable = false
            splits.density.enable = false
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        animationsDisabled true
    }
}

dependencies {
    implementation project(ModulesDependency.CORE)
    implementation project(ModulesDependency.CORE_INFRASTRUCTURE)
    implementation project(ModulesDependency.CORE_DOMAIN)
    implementation project(ModulesDependency.CORE_DATA)
    implementation project(ModulesDependency.CORE_ANALYTICS)

    implementation project(ModulesDependency.LIB_FRAMEWORK)
    implementation project(ModulesDependency.LIB_BASE)

    implementation(KotlinDependency.KOTLIN)

    implementation(AndroidDependency.APP_COMPAT)
    implementation(AndroidDependency.RECYCLER_VIEW)
    implementation(AndroidDependency.CONSTRAINT_LAYOUT)
    implementation AndroidDependency.LIFECYCLE_VIEW_MODEL
    implementation AndroidDependency.LIFECYCLE_RUNTIME
    implementation AndroidDependency.ACTIVITY_API
    implementation AndroidDependency.FRAGMENT_API

    implementation platform(FirebaseBomDependency.FIREBASE_BOM)
    implementation(FirebaseBomDependency.FIREBASE_CRASHLYTICS)

    implementation(GoogleDependency.MATERIAL)
    implementation(GoogleDependency.GSON)

    implementation KoinDependency.KOIN_SCOPE
    implementation KoinDependency.KOIN_VIEW_MODEL

    implementation ArrowDependency.ARROW_CORE
    implementation ArrowDependency.ARROW_SYNTAX
    kapt ArrowDependency.ARROW_META

    implementation(AmplitudeDependency.AMPLITUDE)
    implementation(AmplitudeDependency.OK_HTTP)

    testImplementation(TestDependency.J_UNIT)
    testImplementation TestDependency.MOCKK
    testImplementation TestDependency.ARCH_CORE_TESTING
    testImplementation TestDependency.COROUTINES_TEST

    androidTestImplementation AndroidTestDependency.TEST_RUNNER
    androidTestImplementation AndroidTestDependency.TEST_RULES
    androidTestImplementation AndroidTestDependency.J_UNIT_EXT
    androidTestImplementation AndroidTestDependency.KOIN_TEST
    androidTestImplementation AndroidTestDependency.MOCKK_ANDROID
}
